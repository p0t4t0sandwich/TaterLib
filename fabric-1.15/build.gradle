import net.fabricmc.loom.task.RemapJarTask

plugins {
    id('com.github.johnrengelman.shadow') version('7.1.2')
    id('fabric-loom') version('1.1-SNAPSHOT')
}

archivesBaseName = 'TaterAPI'

repositories {
    maven { url 'https://maven.fabricmc.net/' }
    mavenLocal()
}

dependencies {
    implementation(project(path: ':common', configuration: 'shadow'))

    // Minecraft and Fabric mappings
    minecraft("com.mojang:minecraft:${project.minecraft_version}")
    mappings("net.fabricmc:yarn:${project.yarn_mappings}:v2")
    modImplementation("net.fabricmc:fabric-loader:${project.loader_version}")

    // Fabric API
    Set<String> apiModules = [
            "fabric-api-base",
    ]

    apiModules.forEach {
        modImplementation(fabricApi.module(it, project.fabric_version))
    }
}

processResources {
    inputs.property("version", project.version)
    inputs.property("minecraft_version", project.minecraft_version)
    inputs.property("loader_version", project.loader_version)
    filteringCharset("UTF-8")

    filesMatching("fabric.mod.json") {
        expand("version": project.version,
                "minecraft_version": project.minecraft_version,
                "loader_version": project.loader_version)
    }
}

shadowJar {
    dependencies {
        exclude('net.fabricmc:.*')
        include(project(":common"))
        exclude('/mappings/*')
    }
    relocate('com.google.gson', 'dev.neuralnexus.taterapi.lib.gson')

    archiveName("TaterAPI-${version}-fabric-${minecraft_version}-dev.jar")
    classifier('')
}

task remappedShadowJar(type: RemapJarTask) {
    dependsOn(tasks.shadowJar)
    input.set(tasks.shadowJar.archiveFile)
    addNestedDependencies.set(true)

    destinationDir(file('../target'))
    archiveFileName = "TaterAPI-${version}-fabric-${minecraft_version}.jar"
}
remappedShadowJar.doLast {
    task ->
        ant.checksum(file: task.archivePath) {
            hashAlgorithm = 'md5'
            property = 'md5sum'
        }
}

tasks.assemble.dependsOn(tasks.remappedShadowJar)

artifacts {
    archives(remappedShadowJar)
    archives(shadowJar)
}
