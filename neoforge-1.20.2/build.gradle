plugins {
    id('com.github.johnrengelman.shadow') version('8.1.1')
    id('net.neoforged.gradle.userdev') version('7.0.5')
}

base {
    archivesName = project_id
}

java.toolchain.languageVersion = JavaLanguageVersion.of(java_version)
sourceCompatibility = java_version
targetCompatibility = java_version

// Include resources generated by data generators.
sourceSets.main.resources { srcDir 'src/generated/resources' }

dependencies {
    implementation(project(path: ':common', configuration: 'shadow'))
    compileOnly("net.neoforged:neoforge:${neo_version}")
}

tasks.withType(ProcessResources).configureEach {
    var replaceProperties = [
            minecraft_version      : minecraft_version,
            minecraft_version_range: minecraft_version_range,
            loader_version_range   : loader_version_range,
            forge_version_range    : forge_version_range,
            neo_version            : neo_version,
            neo_version_range      : neo_version_range,
            pack_format_number     : pack_format_number,

            project_id             : project_id,
            project_name           : project_name,
            project_description    : project_description,
            license                : license,
            version                : version,
            authors                : authors,

            issue_tracker_url      : issue_tracker_url,
            update_json_url        : update_json_url,
            display_url            : display_url,
    ]
    inputs.properties replaceProperties

    filesMatching(['META-INF/mods.toml', 'pack.mcmeta']) {
        expand replaceProperties + [project: project]
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

shadowJar {
    dependencies {
        include(project(':common'))
    }

    destinationDirectory = file('../target')
    archiveFileName = "${project_name}-neoforge-${minecraft_version}-${version}.jar"
}
shadowJar.doLast {
    task ->
        ant.checksum(file: task.archivePath) {
            hashAlgorithm = 'md5'
            property = 'md5sum'
        }
}

artifacts {
    archives(shadowJar)
}
