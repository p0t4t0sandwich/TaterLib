import java.time.Instant

plugins {
    id('maven-publish')
    alias(libs.plugins.shadow)
}

dependencies {
    compileOnly(libs.mixin)
    compileOnly(libs.asm.tree)
    compileOnly(project(":modapi:entrypoint-spoof"))
    implementation(project(":api"))
}

jar {
    archiveFileName = "conditional-mixins-${conditional_mixins_version}.jar"
}

shadowJar {
    dependencies {
        include(project(":api"))
        exclude('plugin.yml')
        exclude('bungee.yml')
        exclude('fabric.mod.json')
        exclude('META-INF/mods.toml')
        exclude('META-INF/neoforge.mods.toml')
        exclude('mcmod.info')
        exclude('pack.mcmeta')
        exclude('META-INF/sponge_plugins.json')
        exclude('velocity-plugin.json')
        exclude('TaterLib.png.gz')
    }

    minimize() {
        exclude('dev.neuralnexus.conditionalmixins')
    }

    archiveFileName = "conditional-mixins-${conditional_mixins_version}-full.jar"

    manifest {
        attributes 'Specification-Title': 'Conditional Mixins',
                'Specification-Version': conditional_mixins_version,
                'Specification-Vendor': 'NeualNexus',
                'Implementation-Version': conditional_mixins_version,
                'Implementation-Vendor': 'NeualNexus',
                'Implementation-Timestamp': Instant.now().toString()
    }

    from(["README.md"]) {
        into("META-INF")
    }
}

publishing {
    publications {
        shadow(MavenPublication) {
            groupId = group_id
            artifactId = "conditional-mixins"
            version = "${conditional_mixins_version}"

            artifact(shadowJar) {
                classifier = ''
            }

            pom {
                name = 'conditional-mixins'
                description = 'A mixin plugin and annotation set that allow you to apply mixins based on the platform, loaded plugins/mods, and minecraft version'
                url = 'https://github.com/p0t4t0sandwich/TaterLib/tree/main/tooling/conditional-mixins/README.md'
                licenses {
                    license {
                        name = 'MIT'
                        url = 'https://github.com/p0t4t0sandwich/TaterLib/blob/main/LICENSE-API'
                    }
                }
                developers {
                    developer {
                        id = 'p0t4t0sandwich'
                        name = 'Dylan Sperrer'
                        email = 'dylan@neuralnexus.dev'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/p0t4t0sandwich/TaterLib.git'
                    url = 'https://github.com/p0t4t0sandwich/TaterLib'
                }
            }
        }
    }

    repositories {
        mavenLocal()
        maven {
            name = "NeuralNexusReleases"
            url = "https://maven.neuralnexus.dev/releases"
            credentials {
                username = project.findProperty("neuralNexusUsername") ?: System.getenv("NEURALNEXUS_USERNAME")
                password = project.findProperty("neuralNexusPassword") ?: System.getenv("NEURALNEXUS_PASSWORD")
            }
        }
    }
}

artifacts {
    archives(shadowJar)
}
