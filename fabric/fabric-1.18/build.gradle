import net.fabricmc.loom.task.RemapJarTask

plugins {
    alias(libs.plugins.shadow)
    alias(libs.plugins.loom)
}

java.toolchain.languageVersion = JavaLanguageVersion.of(java_version)
sourceCompatibility = targetCompatibility = java_version

loom.mixin.useLegacyMixinAp = false

dependencies {
    minecraft("com.mojang:minecraft:${project.minecraft_version}")
    mappings loom.officialMojangMappings()
    modImplementation("net.fabricmc:fabric-loader:${project.loader_version}")

    Set<String> apiModules = [
            'fabric-command-api-v1',
            'fabric-lifecycle-events-v1',
            'fabric-networking-api-v1'
    ]

    apiModules.forEach {
        modImplementation(fabricApi.module(it, project.fabric_version))
    }

    // Lucko's Permissions API
    modImplementation('me.lucko:fabric-permissions-api:0.2-SNAPSHOT')

    compileOnly(project(':api'))
    compileOnly(project(':common'))
    compileOnly(project(':tooling:loader'))
    implementation(project(':vanilla:vanilla-1.18'))
}

shadowJar {
    dependencies {
        include(project(':vanilla:vanilla-1.18'))
        exclude('/mappings/*')
    }
    relocate("dev.neuralnexus.taterlib.v1_18.vanilla", "dev.neuralnexus.taterlib.v1_18.vanilla.fabric")
}

task remappedShadowJar(type: RemapJarTask) {
    dependsOn(tasks.shadowJar)
    input = tasks.shadowJar.archiveFile
    archiveFileName = "${project_id}-fabric-${minecraft_version}-${version}.jar"
}

tasks.assemble.dependsOn('remappedShadowJar')
