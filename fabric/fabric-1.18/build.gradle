import net.fabricmc.loom.task.RemapJarTask

plugins {
    id('com.github.johnrengelman.shadow') version('8.1.1')
    id('fabric-loom') version('1.3-SNAPSHOT')
}

dependencies {
    implementation(project(path: ':common', configuration: 'shadow'))

    // Minecraft and Fabric mappings
    minecraft("com.mojang:minecraft:${project.minecraft_version}")
    mappings("net.fabricmc:yarn:${project.yarn_mappings}:v2")
    modImplementation("net.fabricmc:fabric-loader:${project.loader_version}")

    // Fabric API
    Set<String> apiModules = [
            'fabric-api-base',
            'fabric-command-api-v1',
            'fabric-lifecycle-events-v1',
            'fabric-networking-api-v1'
    ]

    apiModules.forEach {
        modImplementation(fabricApi.module(it, project.fabric_version))
    }

    // Lucko's Permissions API
    include(modImplementation('me.lucko:fabric-permissions-api:0.2-SNAPSHOT'))
}

tasks.withType(ProcessResources).configureEach {
    var replaceProperties = [
            minecraft_version  : minecraft_version,

            project_id         : project_id,
            project_name       : project_name,
            project_description: project_description,
            license            : license,
            version            : version,
            group_id           : group_id,
            authors            : authors,

            repo_url           : repo_url
    ]
    inputs.properties replaceProperties

    filesMatching(['fabric.mod.json', "${project_id}.mixins.json"]) {
        expand replaceProperties + [project: project]
    }
}

shadowJar {
    dependencies {
        exclude('net.fabricmc:.*')
        include(project(":common"))
        exclude('/mappings/*')
    }

    archiveFileName = "${project_name}-fabric-${minecraft_version}-${version}-dev.jar"
}

task remappedShadowJar(type: RemapJarTask) {
    dependsOn(tasks.shadowJar)
    input.set(tasks.shadowJar.archiveFile)
    addNestedDependencies.set(true)

    destinationDirectory = file('../../target')
    archiveFileName = "${project_name}-fabric-${minecraft_version}-${version}.jar"
}

tasks.assemble.dependsOn(tasks.remappedShadowJar)

artifacts {
    archives(remappedShadowJar)
    archives(shadowJar)
}

