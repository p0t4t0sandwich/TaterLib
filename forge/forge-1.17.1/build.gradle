plugins {
    alias(libs.plugins.shadow)
    alias(libs.plugins.unimined)
}

java.toolchain.languageVersion = JavaLanguageVersion.of(java_version)
sourceCompatibility = targetCompatibility = java_version

unimined.minecraft(sourceSets.main) {
    version(minecraft_version)
    minecraftForge {
        loader(forge_version)
        mixinConfig("taterlib.mixins.v1_17_1.forge.json")
    }
    mappings {
        mojmap()
    }
    defaultRemapJar = false
    remap(shadowJar) {
        prodNamespace("searge")
        mixinRemap {
            disableRefmap()
        }
        archiveFileName = "${project_id}-forge-${minecraft_version}-${project.version}.jar"
    }
}

tasks.build.dependsOn(remapShadowJar)

dependencies {
    compileOnly(project(':api'))
    compileOnly(project(':common'))
    compileOnly(project(':modapi:conditional-mixins'))
    compileOnly(project(':tooling:loader'))
    compileOnly(project(':forge:forge-utils-modern'))
    implementation(project(":vanilla:vanilla-1.17"))
}

shadowJar {
    dependencies {
        include(project(":vanilla:vanilla-1.17"))
    }
    relocate("dev.neuralnexus.taterlib.mixin.v1_17.vanilla", "dev.neuralnexus.taterlib.mixin.v1_17.vanilla.forge")
    relocate("dev.neuralnexus.taterlib.v1_17.vanilla", "dev.neuralnexus.taterlib.v1_17.vanilla.forge")
    archiveFileName = "${project_id}-forge-${minecraft_version}-${version}-dev.jar"
}
